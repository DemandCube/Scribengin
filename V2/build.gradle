allprojects {
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'eclipse'
  apply plugin: 'maven'
  apply plugin: 'maven-publish'
  apply plugin: 'jacoco'

  jacoco {
    toolVersion = "0.6.2.201302030002"
    reportsDir = file("$buildDir/JacocoReportDir")
  }
  
  eclipse {
    classpath {
      downloadSources=true
    }
  }

  eclipse.classpath.defaultOutputDir = file( 'build/classes' )

  group = 'com.neverwinterdp'
  sourceCompatibility = 1.7
  version = '1.0-SNAPSHOT'
  configurations.compile.transitive = true

  project.ext {
    kafkaVersion = "0.8.1.1"
    jettyVersion = "9.2.0.M0"
    zookeeperVersion = "3.4.6"
  }

  repositories {
    mavenLocal()
    mavenCentral()
  }

  dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.5'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.5'
    testCompile group: 'junit', name: 'junit', version: '4.11'
  }

  test {
    forkEvery = 1
    ignoreFailures = true
    testLogging.showStandardStreams = true

    filter {
      includeTestsMatching "*UnitTest"
    }
    testLogging {
      events "passed", "skipped", "failed"
    }
  }

  task testJar(type: Jar) {
    classifier = 'tests'
    from sourceSets.test.output
  }

  configurations {
    tests
    published.extendsFrom tests, archives
  }


  install {
    configuration = configurations.published
  }

  artifacts {
    tests testJar
  }
}
